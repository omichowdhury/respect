###
# respect.js #

This is a module-level docstring, and will be displayed at the top of the module documentation.
Documentation generated by [CoffeeDoc](http://github.com/omarkhan/coffeedoc)
###


Comparator = require './comparator'


class RespectPlugin extends Comparator
  ###
  RespectPlugin
  ###

  KEYWORD: 'respect'

  chaiAssert: (ctx) ->
    keyword = @KEYWORD
    ctx.assert(
      @conform,
      "expected #{'#{this}'} to #{keyword} #{'#{exp}'}",
      "expected #{'#{this}'} not to #{keyword} #{'#{exp}'} (false negative fail)",
      @expected,
      @displayActual
    )

  shouldAssert: (ctx) ->
    ctx.params =
      operator: "to #{ @KEYWORD }"
      expected: @expected
    if !@conform
      ctx.fail()

  ###
  STATICS
  ###

  @parseCustom: (rawOptions) ->
    alias = rawOptions?.alias
    options = {}
    for optionName, defaultValue of @DEFAULT_OPTIONS
      options[optionName] = rawOptions?[optionName]
      options[optionName] ?= defaultValue
    return [alias, options]

  @spawnSubClass: (options) ->
    class ComparatorClass extends @
    [alias, options] = @parseCustom(options)
    ComparatorClass::KEYWORD = alias if alias
    ComparatorClass.DEFAULT_OPTIONS = options
    return ComparatorClass

  @chaiPlugin: (options) ->
    (chai, utils) =>
      ComparatorClass = @spawnSubClass options
      utils.addMethod chai.Assertion.prototype, ComparatorClass::KEYWORD, (expected, options) ->
        comparator = new ComparatorClass(expected, this._obj, options)
        comparator.chaiAssert(this)

  @shouldPlugin: (options) ->
    (should, Assertion) =>
      ComparatorClass = @spawnSubClass options
      Assertion.add ComparatorClass::KEYWORD, (expected, options) ->
        comparator = new ComparatorClass(expected, this.obj, options)
        comparator.shouldAssert(this)


exports = module.exports = RespectPlugin
